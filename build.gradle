plugins {
    id 'java'
    id 'signing'
    id 'eclipse'
    id 'jacoco'
    id 'net.nemerosa.versioning' version '3.1.0'
}

configurations {
    asciidoclet
}

group = 'net.reini'
description = 'Provides a javac annotation processor checking for potential missing JUnit 4 test methods not annotated with @Test'

repositories {
    mavenCentral()
}

java {
  sourceCompatibility = JavaVersion.VERSION_21
  withJavadocJar()
  withSourcesJar()
}

compileTestJava {
    options.compilerArgs = [ 
//       "-processor", "net.reini.javac.JUnit4ClassProcessor"
    ]
 }

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.36'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.easymock:easymock:5.6.0'
}

signing {
    required = (project.hasProperty('signing.password') && !project.version.contains('-SNAPSHOT'))
    sign configurations.archives
}

jar {
    manifest {
        attributes 'Implementation-Title': 'JUnit compatibility classes',
                   'Implementation-Version': version,
                   'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                   'Build-Revision': versioning.info.commit,
                   'Specification-Title': project.name,
                   'Specification-Version': project.version,
                   'Specification-Vendor': 'reini.net'
        metaInf {
            from file('.')
            include 'LICENSE'
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

check.dependsOn jacocoTestReport
